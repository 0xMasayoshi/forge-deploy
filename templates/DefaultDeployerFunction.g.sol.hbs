// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

import "./Deployer.g.sol";
import "proxy/ForgeDeploy_EIP173Proxy.sol";

struct DeployOptions {
    bool overrideIfExist;
    string proxyOnTag;
    address proxyOwner;
}

library DefaultDeployerFunction{

    Vm constant vm = Vm(address(bytes20(uint160(uint256(keccak256("hevm cheat code"))))));

    function _useExisting(Deployer deployer, string memory name, DeployOptions memory options) private view returns (address) {
        if (!options.overrideIfExist) {            
            address existing = deployer.getAddress(name);
            if (existing != address(0)) {
                return existing;
            }
        }
        return address(0);
    }

    function _contextHasTag(string memory tag) internal returns (bool) {
        if (bytes(tag).length == 0) {
            return false;
        }
        return true;
    }

    function deploy(
        Deployer deployer,
        string memory name,
        string memory artifact,
        bytes memory args,
        DeployOptions memory options
    ) internal returns (address deployed) {
        if (_contextHasTag(options.proxyOnTag)) {
            Deployment memory existing = deployer.get(name);
            bytes memory bytecode = bytes.concat(vm.getCode(artifact), args);

            string memory implName = string.concat(name, "_Implementation");
            if (existing.addr != address(0)) {
                address implementation;
                Deployment memory existingImpl = deployer.get(implName);
                if (
                    existingImpl.addr == address(0) || 
                    keccak256(bytes.concat(existingImpl.bytecode, existingImpl.args)) != keccak256(bytes.concat(bytecode, args))
                ) {
                    // TODO implementation args
                    implementation = deploy(deployer, implName, artifact, args, DeployOptions({
                        overrideIfExist: true,
                        proxyOnTag: "",
                        proxyOwner: address(0)
                    }));
                } else {
                    implementation = existingImpl.addr;
                }
                deployed = existing.addr;
                vm.broadcast(options.proxyOwner);
                // TODO extra call data (upgradeToAndCall)
                EIP173Proxy(payable(deployed)).upgradeTo(implementation);
            } else {
                address implementation = deploy(deployer, implName, artifact, args, DeployOptions({
                    overrideIfExist: true,
                    proxyOnTag: "",
                    proxyOwner: address(0)
                }));
                
                // TODO extra call data
                bytes memory proxyArgs = abi.encode(implementation, options.proxyOwner, bytes(""));
                deployed = deploy(deployer, name, "ForgeDeploy_EIP173Proxy.sol:EIP173Proxy", proxyArgs, DeployOptions({
                    overrideIfExist: false,
                    proxyOnTag: "",
                    proxyOwner: address(0)
                }));
            }
        } else {
            address useExisting = _useExisting(deployer, name, options);
            if (useExisting == address(0)) {
                bytes memory bytecode = bytes.concat(vm.getCode(artifact), args);
                vm.broadcast();
                assembly {
                    deployed := create(0, add(bytecode, 0x20), mload(bytecode))
                }
                deployer.save(name, deployed, bytecode, args, artifact);
            }
        }
        
    }
}